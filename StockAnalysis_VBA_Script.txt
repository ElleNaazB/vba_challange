Sub StockTicker()

    Dim ws As Worksheet
    Dim YearlyChange As Double
    Dim TotalValue As Double
    Dim OpenPrice As Double
    Dim ClosePrice As Double
    Dim RowCount As Long
    Dim OutputRow As Long
    Dim i As Long
    Dim TickerLastRow As Long
    Dim GreatestIncrease As Double
    Dim GreatestDecrease As Double
    Dim GreatestTotal As Double
    Dim TickerGreatestIncrease As String
    Dim TickerGreatestDecrease As String
    Dim TickerGreatestTotal As String
    Dim PercentChange As Double

    ' Loop through all worksheets
    
    For Each ws In Worksheets
        With ws
            .Activate

            ' Setting up headers
            .Range("I1").Value = "Ticker"
            .Range("J1").Value = "Yearly Change"
            .Range("K1").Value = "Percent Change"
            .Range("L1").Value = "Total Stock Volume"
            .Range("I1:L1").Font.Bold = True
            .Columns("I:L").AutoFit

            ' Additional headers for second part of project
            .Cells(2, "O").Value = "Greatest % Increase"
            .Cells(3, "O").Value = "Greatest % Decrease"
            .Cells(4, "O").Value = "Greatest Total Volume"
            .Cells(1, "P").Value = "Ticker"
            .Cells(1, "Q").Value = "Value"
            .Range("P1:Q1").Font.Bold = True
            .Range("O1:O4").Font.Bold = True

            ' Initializing variables
            
            TotalValue = 0
            OutputRow = 2
            OpenPrice = .Cells(2, "C").Value
            RowCount = .Cells(.Rows.Count, "A").End(xlUp).Row

            ' Looping through rows
            
            For i = 2 To RowCount
                TotalValue = TotalValue + .Cells(i, "G").Value

                ' Checking if the stock ticker has changed
                
                If .Cells(i, "A").Value <> .Cells(i + 1, "A").Value Then
                    ClosePrice = .Cells(i, "F").Value
                    YearlyChange = ClosePrice - OpenPrice

                    If OpenPrice <> 0 Then
                        PercentChange = YearlyChange / OpenPrice
                    Else
                        PercentChange = 0
                    End If

                    ' Recording the calculated data
                    .Cells(OutputRow, "K").NumberFormat = "0.00%"
                    .Cells(OutputRow, "I").Value = .Cells(i, "A").Value
                    .Cells(OutputRow, "J").Value = YearlyChange
                    .Cells(OutputRow, "K").Value = PercentChange
                    .Cells(OutputRow, "L").Value = TotalValue

                    ' Color coding for Yearly Change
                    
                    If YearlyChange > 0 Then
                        .Cells(OutputRow, "J").Interior.ColorIndex = 4
                    ElseIf YearlyChange < 0 Then
                        .Cells(OutputRow, "J").Interior.ColorIndex = 3
                    Else
                        .Cells(OutputRow, "J").Interior.ColorIndex = 0
                    End If

                    ' Preparing for next ticker
                    
                    TotalValue = 0
                    OutputRow = OutputRow + 1
                    If i + 1 <= RowCount Then
                        OpenPrice = .Cells(i + 1, "C").Value
                    End If
                End If
            Next i
        End With
    Next ws

    ' Finding Greatest Increase, Decrease, and Total Volume
    
    For Each ws In Worksheets
        With ws
            TickerLastRow = .Cells(.Rows.Count, "I").End(xlUp).Row
            For i = 2 To TickerLastRow
                If .Cells(i, "K").Value > GreatestIncrease Then
                    GreatestIncrease = .Cells(i, "K").Value
                    TickerGreatestIncrease = .Cells(i, "I").Value
                ElseIf .Cells(i, "K").Value < GreatestDecrease Then
                    GreatestDecrease = .Cells(i, "K").Value
                    TickerGreatestDecrease = .Cells(i, "I").Value
                ElseIf .Cells(i, "L").Value > GreatestTotal Then
                    GreatestTotal = .Cells(i, "L").Value
                    TickerGreatestTotal = .Cells(i, "I").Value
                End If
                
            Next i
            
            .Range("Q2:Q3").NumberFormat = "0.00%"
            .Range("P2").Value = TickerGreatestIncrease
            .Range("P3").Value = TickerGreatestDecrease
            .Range("P4").Value = TickerGreatestTotal
            .Range("Q2").Value = GreatestIncrease
            .Range("Q3").Value = GreatestDecrease
            .Range("Q4").Value = GreatestTotal
            
        End With
    Next ws
End Sub
